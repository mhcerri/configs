#!/usr/bin/perl
use strict;
use File::Basename;

my %args = (
    domain  => undef,
    verbose => 0,
);

sub debug($) {
	if ($args{verbose}) {
		print STDERR $_[0];
	}
}

sub error($) {
    print STDERR $_[0];
}

# Parse arguments
while (@ARGV) {
    my $arg = shift @ARGV;
    if ($arg =~ /^(-h|--help)$/) {
        error "Usage: \n";
        error "    " . basename($0) . " [OPTIONS] DOMAIN\n";
        error "Options: \n";
        error "    -q, --quiet\n";
        error "    -v, --verbose\n";
        exit(0);
    }
    elsif ($arg =~ /^(-q|--quiet)$/) {
        $args{verbose} = 0;
    }
    elsif ($arg =~ /^(-v|--verbose)$/) {
        $args{verbose} = 1;
    }
    elsif ($arg =~ /^-/) {
        error "Invalid argument: $arg.\n";
        exit(1);
    }
    else {
	if (defined $args{domain}) {
		error "Multiple domains are not supported.\n";
		exit(1);
	}
	($args{domain}, $args{user}) = reverse split(/@/, $arg, 2);
    }
}

if (!defined $args{domain}) {
	error "Missing domain.\n";
	exit(1);
}

# Wait for IP
my $waiting = 0;
my $ipv4 = undef;
while (!defined $ipv4) {
	# Read virsh domifaddr outout
	open(OUT, "virsh domifaddr \"$args{domain}\" |") or die "Error: $!";
	while (<OUT>) {
		if (/^\s*\w+\s+[^\s]+\s+ipv4\s+(\d+\.\d+\.\d+\.\d+)(\/.*)?\s*$/) {
			$ipv4 = $1;
			last;
		}
	}
	close(OUT) or die "Error: $!";
	if (!defined $ipv4) {
		if (!$waiting) {
			debug("Waiting for IP address of domain \"$args{domain}\" ");
			$waiting = 1;
		} else {
			debug(".");
		}
		sleep(1);
	} elsif ($waiting) {
		debug("\n");
	}
}
debug("Found IP address $ipv4 for domain \"$args{domain}\".\n");
print "$ipv4\n";
